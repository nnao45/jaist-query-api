set dotenv-load := true
NAME := `sed -n 2p ./Cargo.toml | cut -c 8- | xargs printf`
VERSION := `sed -n 3p ./Cargo.toml | cut -c 11- | sed '1s/^/v/' | xargs printf`
GIT_HASH := `git rev-parse HEAD`

DOCKER_REPO := "nnao45"
MYSQL_DOCKER := "jqapi-test-mysql"
MYSQL_DOCKER_EXISTS_FLAG := `if [ ! -z ${CIRCLECI:-} ]; then echo 1; exit 0; fi; docker ps --format "{{ .Names }}" --filter "name=jqapi-test-mysql" | wc -l`
MYSQL_VERSION := "8.0.27"

run:
  cargo run

docker-run:
  docker run -it --rm -p 8081:8081 --env-file=./.env {{ DOCKER_REPO }}/{{ NAME }}:{{ GIT_HASH }}

build:
  cargo build --release

docker-login:
    docker login

docker-app-build:
	docker rmi -f {{ DOCKER_REPO }}/{{ NAME }}:latest
	docker build -t {{ DOCKER_REPO }}/{{ NAME }}:latest .
	docker tag {{ DOCKER_REPO }}/{{ NAME }}:latest {{ DOCKER_REPO }}/{{ NAME }}:{{ GIT_HASH }}

docker-app-push: docker-login
	docker push {{ DOCKER_REPO }}/{{ NAME }}:latest
	docker push {{ DOCKER_REPO }}/{{ NAME }}:{{ GIT_HASH }}

docker-ddl-build:
	docker rmi -f {{ DOCKER_REPO }}/ddl-docker:latest
	docker build -t {{ DOCKER_REPO }}/ddl-docker:latest . -f=tools/ddl_docker/Dockerfile
	docker tag {{ DOCKER_REPO }}/ddl-docker:latest {{ DOCKER_REPO }}/ddl-docker:{{ GIT_HASH }}

docker-ddl-push: docker-login
	docker push {{ DOCKER_REPO }}/ddl-docker:latest
	docker push {{ DOCKER_REPO }}/ddl-docker:{{ GIT_HASH }}

docker-app-release: docker-app-build docker-app-push
docker-ddl-release: docker-ddl-build docker-ddl-push

mysql-db-is-exist:
    #!/bin/bash
    echo MYSQL_DOCKER_EXISTS_FLAG is {{ MYSQL_DOCKER_EXISTS_FLAG }}
    if [ ! {{ MYSQL_DOCKER_EXISTS_FLAG }} = 0 ]; then
        echo "{{ MYSQL_DOCKER }} is exists"
    else
        echo "{{ MYSQL_DOCKER }} is not exists"
    fi

check-mysql-db:
    #!/bin/bash
    STATUS=1
    COUNT=0

    while [ ${STATUS} = 1 ]
    do
        mysqladmin ping -u root -h ${MYSQL_HOST} -p${MYSQL_PORT} > /dev/null 2>&1 && STATUS=0
        if [ ${STATUS} = 1 ]; then
            echo -n '.'
        fi
        sleep 1
        (( COUNT++ ))
        if [ ${COUNT} = 50 ]; then
            echo "Sorry, Cannot connect the mysql docker."
            exit 1
        fi
    done

    echo '!'
    echo {{ MYSQL_DOCKER }} is alive

lunch-mysql-db:
    #!/bin/bash
    if [ ! {{ MYSQL_DOCKER_EXISTS_FLAG }} = 0 ]; then
        echo "{{ MYSQL_DOCKER }} is exists. Not work."
    else
        echo 'set up the mysql docker'
        docker run --name {{ MYSQL_DOCKER }} \
            --rm \
            -d \
            -e MYSQL_ROOT_PASSWORD=${MYSQL_PASSWORD} \
            -p ${MYSQL_PORT}:${MYSQL_PORT} mysql:{{ MYSQL_VERSION }} \
            --character-set-server=utf8mb4 \
            --default-authentication-plugin=mysql_native_password \
            --collation-server=utf8mb4_unicode_ci
    fi

setup-mysql-db:
    echo "CREATE DATABASE IF NOT EXISTS ${MYSQL_DB} DEFAULT CHARACTER SET utf8mb4 COLLATE utf8mb4_general_ci" | mysql -h${MYSQL_HOST} -uroot -p${MYSQL_PASSWORD} 2>/dev/null
    echo 'CREATE USER '\'admin\''@'\'%\'' IDENTIFIED BY '\'${MYSQL_PASSWORD}\''' | mysql -h${MYSQL_HOST} -uroot -p${MYSQL_PASSWORD} 2>/dev/null
    echo 'GRANT ALL On *.* To admin@'\'%\'';' | mysql -h${MYSQL_HOST} -uroot -p${MYSQL_PASSWORD} 2>/dev/null
    echo 'set global time_zone = "Asia/Tokyo"' | mysql -h${MYSQL_HOST} -u${MYSQL_USER} -p${MYSQL_PASSWORD} 2>/dev/null
    diesel migration run

inject-data-mysql-db:
    mysql -h${MYSQL_HOST} -uroot -p${MYSQL_PASSWORD} -D${MYSQL_DB} < ./tools/db_data/fulldb-11-12-2021-15-57-beta.sql

run-test-query-mysql-db:
    mysql -h${MYSQL_HOST} -uroot -p${MYSQL_PASSWORD} -D${MYSQL_DB} < ./tools/query/test_query_1.sql

run-mysql-db: lunch-mysql-db check-mysql-db setup-mysql-db inject-data-mysql-db

clean-mysql-db:
    #!/bin/bash
    if [ {{ MYSQL_DOCKER_EXISTS_FLAG }} = 0 ]; then
        echo "{{ MYSQL_DOCKER }} is not exists. Not work."
    else
        docker kill {{ MYSQL_DOCKER }} > /dev/null && echo 'clean up the mysql docker'
    fi
